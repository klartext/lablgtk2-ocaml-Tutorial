
<html><head><title>Progress Bars</title><meta name="GENERATOR" content="Modular DocBook HTML Stylesheet Version 1.7"/><link rel="HOME" title="GTK+ 2.0 Tutorial using Ocaml" href="book1.html"/><link rel="UP" title="Miscellaneous Widgets" href="c953.html"/><link rel="PREVIOUS" title="The Tooltips Object" href="x1016.html"/><link rel="NEXT" title="Dialogs" href="x1071.html"/></head><body class="SECT1" bgcolor="#FFFFFF" text="#000000" link="#0000FF" vlink="#840084" alink="#0000FF"><div class="NAVHEADER"><table summary="Header navigation table" width="100%" border="0" cellpadding="0" cellspacing="0"><tr><th colspan="3" align="center">GTK+ 2.0 Tutorial using Ocaml</th></tr><tr><td width="10%" align="left" valign="bottom"><a href="x1016.html" accesskey="P">&#60;&#60;&#60; Previous</a></td><td width="80%" align="center" valign="bottom">Miscellaneous Widgets</td><td width="10%" align="right" valign="bottom"><a href="x1071.html" accesskey="N">Next &#62;&#62;&#62;</a></td></tr></table><hr align="LEFT" width="100%"/></div><div class="SECT1"><h1 class="SECT1"><a name="SEC-PROGRESSBARS">Progress Bars</a></h1><p>Progress bars are used to show the status of an operation. They are
pretty easy to use, as you will see with the code below. But first
lets start out with the calls to create a new progress bar using
<a href="http://lablgtk.forge.ocamlcore.org/refdoc/GRange.html#VALprogress_bar" target="_top"><tt class="LITERAL">GRange.progress_bar</tt></a>.</p><table border="0" bgcolor="#E0E0E0" width="100%"><tr><td><pre class="PROGRAMLISTING">val GRange.progress_bar:
	?orientation:Gtk.Tags.progress_bar_orientation -&#62;
	?pulse_step:float -&#62;
	?packing:(GObj.widget -&#62; unit) -&#62;
	?show:bool -&#62; unit -&#62; progress_bar

orientation : default value is `LEFT_TO_RIGHT
pulse_step : default value is 0.1</pre></td></tr></table><p>Now that the progress bar has been created we can use it.</p><table border="0" bgcolor="#E0E0E0" width="100%"><tr><td><pre class="PROGRAMLISTING">method set_fraction : float -&#62; unit</pre></td></tr></table><p>The argument is the amount "completed", meaning the amount the
progress bar has been filled from 0-100%. This is passed to the
method as a float number ranging from 0.0 to 1.0</p><p>GTK v1.2 has added new functionality to the progress bar that enables
it to display its value in different ways, and to inform the user of
its current value and its range.</p><p>A progress bar may be set to one of a number of orientations using the
function</p><table border="0" bgcolor="#E0E0E0" width="100%"><tr><td><pre class="PROGRAMLISTING">method set_orientation : Gtk.Tags.progress_bar_orientation -&#62; unit</pre></td></tr></table><p>The <tt class="LITERAL">orientation</tt> argument may take one of the following
values to indicate the direction in which the progress bar moves:</p><table border="0" bgcolor="#E0E0E0" width="100%"><tr><td><pre class="PROGRAMLISTING">  `LEFT_TO_RIGHT
  `RIGHT_TO_LEFT
  `BOTTOM_TO_TOP
  `TOP_TO_BOTTOM</pre></td></tr></table><p>As well as indicating the amount of progress that has occured, the
progress bar may be set to just indicate that there is some activity. 
This can be useful in situations where progress cannot be measured against 
a value range. The following function indicates that some progress has been 
made.</p><table border="0" bgcolor="#E0E0E0" width="100%"><tr><td><pre class="PROGRAMLISTING">method pulse : unit -&#62; unit</pre></td></tr></table><p>The step size of the activity indicator is set using the following 
function.</p><table border="0" bgcolor="#E0E0E0" width="100%"><tr><td><pre class="PROGRAMLISTING">method set_pulse_step : float -&#62; unit</pre></td></tr></table><p>When not in activity mode, the progress bar can also display a
configurable text string within its trough, using the following
function.</p><table border="0" bgcolor="#E0E0E0" width="100%"><tr><td><pre class="PROGRAMLISTING">method set_text : string -&#62; unit</pre></td></tr></table><p>You can turn off the display of the string by calling set_text method
again with empty string as an argument.</p><p>The current text setting of a progressbar can be retrieved with the 
following function.</p><table border="0" bgcolor="#E0E0E0" width="100%"><tr><td><pre class="PROGRAMLISTING">method text : string</pre></td></tr></table><p>Progress Bars are usually used with timeouts or other such functions
(see section on <a href="c1941.html">Timeouts and Idle Functions</a>) 
to give the illusion of multitasking. All will employ the
set_fraction or pulse methods in the 
same manner.</p><p>Here is an example of the progress bar, updated using timeouts. This
code also shows you how to reset the Progress Bar.</p><p><span class="INLINEMEDIAOBJECT"><img src="images/progressbar.png"/></span></p><table border="0" bgcolor="#E0E0E0" width="100%"><tr><td><pre class="PROGRAMLISTING">(* file: progressbar.ml *)

let pulse_mode = ref false

(* Update the value of the progress bar so that we get
 * some movement *)
let progress_timeout pbar () =
  if !pulse_mode
  then pbar#pulse ()
  else (
    (* Calculate the value of the progress bar using the
     * value range set in the adjustment object *)
    let new_val =
      let v = pbar#fraction +. 0.01 in
      if v &#62; 1.0 then 0.0 else v
    in
      pbar#set_fraction new_val
  );

  (* As this is a timeout function, return true so that it
   * continues to get called *)
  true

(* Callback that toggles the text display with the progress bar trough *)
let toggle_show_text pbar () =
  let text = pbar#text in
  if text = ""
  then pbar#set_text "some text"
  else pbar#set_text ""

(* Callback that toggles the activity mode of the progress bar *)
let toggle_activity_mode pbar () =
  pulse_mode := not !pulse_mode;
  if !pulse_mode
  then pbar#pulse ()
  else pbar#set_fraction 0.0

(* Callback that toggles the orientation of the progress bar *)
let toggle_orientation pbar () =
  match pbar#orientation with
  | `LEFT_TO_RIGHT -&#62; pbar#set_orientation `RIGHT_TO_LEFT
  | `RIGHT_TO_LEFT -&#62; pbar#set_orientation `LEFT_TO_RIGHT
  | _ -&#62; ()

(* Remove timer and quit *)
let destroy_progress timer () =
  GMain.Timeout.remove timer;
  GMain.Main.quit ()

let main () =
  let window = GWindow.window ~title:"ProgressBar" () in

  let vbox = GPack.vbox ~border_width:10 ~packing:window#add () in

  (* Create a centering alignment object *)
  let align = GBin.alignment ~xalign:0.5 ~yalign:0.5
    ~xscale:0.0 ~yscale:0.0 ~packing:vbox#add ()
  in

  (* Create the progressbar *)
  let pbar = GRange.progress_bar ~packing:align#add () in

  (* Add a timer callback to update the value of the progress bar *)
  let timer = GMain.Timeout.add ~ms:100 ~callback:(progress_timeout pbar) in
  GMisc.separator `HORIZONTAL ~packing:vbox#add ();

  let table = GPack.table ~rows:3 ~columns:1 ~homogeneous:false
    ~packing:vbox#add ()
  in

  (* Add a check button to select displaying of trough text *)
  let check = GButton.check_button ~label:"Show text"
    ~packing:(table#attach ~left:0 ~top:0) ()
  in
  check#connect#clicked ~callback:(toggle_show_text pbar);
  
  (* Add a check button to toggle activity mode *)
  let check = GButton.check_button ~label:"Activity mode"
    ~packing:(table#attach ~left:0 ~top:1) ()
  in
  check#connect#clicked ~callback:(toggle_activity_mode pbar);

  (* Add a check button to toggle orientation *)
  let check = GButton.check_button ~label:"Right to Left"
    ~packing:(table#attach ~left:0 ~top:2) ()
  in
  check#connect#clicked ~callback:(toggle_orientation pbar);

  (* Add a buton to exit the program *)
  let button = GButton.button ~label:"Close" ~packing:vbox#add () in
  button#connect#clicked ~callback:(destroy_progress timer);

  window#connect#destroy ~callback:(destroy_progress timer);
  window#show ();
  GMain.Main.main ()

let _ = main ()</pre></td></tr></table></div><div class="NAVFOOTER"><hr align="LEFT" width="100%"/><table summary="Footer navigation table" width="100%" border="0" cellpadding="0" cellspacing="0"><tr><td width="33%" align="left" valign="top"><a href="x1016.html" accesskey="P">&#60;&#60;&#60; Previous</a></td><td width="34%" align="center" valign="top"><a href="book1.html" accesskey="H">Home</a></td><td width="33%" align="right" valign="top"><a href="x1071.html" accesskey="N">Next &#62;&#62;&#62;</a></td></tr><tr><td width="33%" align="left" valign="top">The Tooltips Object</td><td width="34%" align="center" valign="top"><a href="c953.html" accesskey="U">Up</a></td><td width="33%" align="right" valign="top">Dialogs</td></tr></table></div></body></html>