<table>
<thead>
<tr class="header">
<th align="left">GTK+ 2.0 Tutorial using Ocaml</th>
</tr>
</thead>
<tbody>
<tr class="odd">
<td align="left"><a href="x1307.html">&lt;&lt;&lt; Previous</a></td>
<td align="left">Miscellaneous Widgets</td>
<td align="left"><a href="x1415.html">Next &gt;&gt;&gt;</a></td>
</tr>
</tbody>
</table>
<hr />
<h1 class="SECT1">Color Selection</h1>
<p>The color selection widget is, not surprisingly, a widget for interactive selection of colors. This composite widget lets the user select a color by manipulating RGB (Red, Green, Blue) and HSV (Hue, Saturation, Value) triples. This is done either by adjusting single values with sliders or entries, or by picking the desired color from a hue-saturation wheel/value bar. Optionally, the opacity of the color can also be set.</p>
<p>Lets have a look at what the color selection widget has to offer us. The widget comes in two flavours: <a href="http://lablgtk.forge.ocamlcore.org/refdoc/GMisc.html#VALcolor_selection"><code class="LITERAL">GMisc.color_selection</code></a> and <a href="http://lablgtk.forge.ocamlcore.org/refdoc/GWindow.html#VALcolor_selection_dialog"><code class="LITERAL">GWindow.color_selection_dialog</code></a>.</p>
<pre class="PROGRAMLISTING"><code>val GMisc.color_selection :
    ?alpha:int -&gt;
    ?color:Gdk.color -&gt;
    ?has_opacity_control:bool -&gt;
    ?has_palette:bool -&gt;
    ?border_width:int -&gt;
    ?width:int -&gt;
    ?height:int -&gt;
    ?packing:(GObj.widget -&gt; unit) -&gt;
    ?show:bool -&gt; unit -&gt; color_selection</code></pre>
<p>You'll probably not be using this constructor directly. It creates an orphan color_selection widget which you'll have to parent yourself. The color_selection widget inherits from the VBox widget.</p>
<pre class="PROGRAMLISTING"><code>val GWindow.color_selection_dialog :
    ?title:string -&gt;
    ?parent:#window_skel -&gt;
    ?destroy_with_parent:bool -&gt;
    ?allow_grow:bool -&gt;
    ?allow_shrink:bool -&gt;
    ?icon:GdkPixbuf.pixbuf -&gt;
    ?modal:bool -&gt;
    ?screen:Gdk.screen -&gt;
    ?type_hint:Gdk.Tags.window_type_hint -&gt;
    ?position:Gtk.Tags.window_position -&gt;
    ?wm_name:string -&gt;
    ?wm_class:string -&gt;
    ?border_width:int -&gt;
    ?width:int -&gt;
    ?height:int -&gt;
    ?show:bool -&gt; unit -&gt; color_selection_dialog</code></pre>
<p>This is the most common color selection constructor. It creates a color_selection_dialog. It consists of a Frame containing a color_selection widget, an HSeparator and an HBox with three buttons, &quot;Ok&quot;, &quot;Cancel&quot; and &quot;Help&quot;. You can reach these buttons by accessing the &quot;ok_button&quot;, &quot;cancel_button&quot; and &quot;help_button&quot; methods in the color_selection_dialog object, (i.e., <code class="LITERAL">color_sel_dialog#ok_button</code>).</p>
<pre class="PROGRAMLISTING"><code>method set_has_opacity_control : bool -&gt; unit</code></pre>
<p>The color selection widget supports adjusting the opacity of a color (also known as the alpha channel). This is disabled by default. Calling this function with has_opacity set to true enables opacity. Likewise, has_opacity set to false will disable opacity.</p>
<pre class="PROGRAMLISTING"><code>method set_color : Gdk.color -&gt; unit
method set_alpha : int -&gt; unit</code></pre>
<p>You can set the current color explicitly by calling set_color method with a GdkColor. Setting the opacity (alpha channel) is done with set_alpha method. The alpha value should be between 0 (fully transparent) and 65636 (fully opaque).</p>
<pre class="PROGRAMLISTING"><code>method color : Gdk.color
method alpha : int</code></pre>
<p></p>
<p>Here's a simple example demonstrating the use of the color_selection_dialog. The program displays a window containing a drawing area. Clicking on it opens a color selection dialog, and changing the color in the color selection dialog changes the background color.</p>
<p><img src="images/colorsel.png" /></p>
<pre class="PROGRAMLISTING"><code>(* file: colorsel.ml *)

let dialog_ref = ref None
let color = ref (`RGB (0, 65535, 0))     (* GDraw.color ref type *)

(* &quot;color_changed&quot; event does not exist in lablgtk2!!! *)
(*
let color_changed_cb colorsel drawingarea () =
  let ncolor = colorsel#color in
  drawingarea#misc#modify_bg [(`NORMAL, `COLOR ncolor)]
*)

let response dlg drawingarea resp =
  let colorsel = dlg#colorsel in
  begin
  match resp with
  | `OK -&gt; color := `COLOR colorsel#color
  | _ -&gt; ()
  end;
  drawingarea#misc#modify_bg [(`NORMAL, !color)];
  dlg#misc#hide ()

(* Drawingarea button_press event handler *)
let button_pressed drawingarea ev =
  (* Create color selection dialog *)
  let colordlg =
    match !dialog_ref with
    | None -&gt;
      let dlg = GWindow.color_selection_dialog ~title:&quot;Select background color&quot; () in
      dialog_ref := Some dlg;
      dlg
    | Some dlg -&gt; dlg
  in

  (* Get the ColorSelection widget *)
  let colorsel = colordlg#colorsel in

  (* set_prev_color does not exist in lablgtk2!!! *)
  (* colorsel#set_prev_color (GDraw.color !color); *)
  colorsel#set_color (GDraw.color !color); (* requires Gdk.color type *)
  colorsel#set_has_palette true;

  (* Connect to the &quot;color_changed&quot; signal *)
  (* This event does not exist in lablgtk2!!! *)
  (* Need confirm to lablgtk2 team. *)
  (* colorsel#connect#color_changed ~callback:(color_changed_cb colorsel drawingarea);
  *)

  colordlg#connect#response ~callback:(response colordlg drawingarea);

  (* Show the dialog *)
  colordlg#run ();
  true
    
let main () =
  (* Create toplevel window, set title and policies (allow_grow, allow_shrink) *)
  let window = GWindow.window ~title:&quot;Color selection test&quot; ~border_width:10
    ~allow_grow:true ~allow_shrink:true () in

  (* Attach &quot;destroy&quot; events so we can exit *)
  window#connect#destroy ~callback:GMain.Main.quit;

  (* Create drawingarea, set size and catch button events *)
  let drawingarea = GMisc.drawing_area ~width:200 ~height:200 ~packing:window#add () in
  drawingarea#misc#modify_bg [(`NORMAL, !color)];
  drawingarea#event#add [`BUTTON_PRESS];
  drawingarea#event#connect#button_press ~callback:(button_pressed drawingarea);

  window#show ();
  GMain.Main.main ()

let _ = Printexc.print main ()</code></pre>
<hr />
<table>
<tbody>
<tr class="odd">
<td align="left"><a href="x1307.html">&lt;&lt;&lt; Previous</a></td>
<td align="left"><a href="book1.html">Home</a></td>
<td align="left"><a href="x1415.html">Next &gt;&gt;&gt;</a></td>
</tr>
<tr class="even">
<td align="left">Calendar</td>
<td align="left"><a href="c953.html">Up</a></td>
<td align="left">File Selections</td>
</tr>
</tbody>
</table>
