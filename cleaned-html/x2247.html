<table>
<thead>
<tr class="header">
<th align="left">GTK+ 2.0 Tutorial using Ocaml</th>
</tr>
</thead>
<tbody>
<tr class="odd">
<td align="left"><a href="x2205.html">&lt;&lt;&lt; Previous</a></td>
<td align="left">Scribble, A Simple Example Drawing Program</td>
<td align="left"><a href="c2294.html">Next &gt;&gt;&gt;</a></td>
</tr>
</tbody>
</table>
<hr />
<h1 class="SECT1">The DrawingArea Widget, And Drawing</h1>
<p>We now turn to the process of drawing on the screen. The widget we use for this is the DrawingArea widget. A drawing area widget is essentially an X window and nothing more. It is a blank canvas in which we can draw whatever we like. A drawing area is created using the call:</p>
<pre class="PROGRAMLISTING"><code>val GMisc.drawing_area :
    ?width:int -&gt;
    ?height:int -&gt;
    ?packing:(GObj.widget -&gt; unit) -&gt;
    ?show:bool -&gt;
    unit -&gt; drawing_area</code></pre>
<p>The arguments <code class="LITERAL">width</code> and <code class="LITERAL">height</code> specifies the default size of the drawing area.</p>
<p>The default size can be overridden, as is true for all widgets, by calling <code class="LITERAL">misc#set_size_request</code> method, and that, in turn, can be overridden if the user manually resizes the the window containing the drawing area.</p>
<p>It should be noted that when we create a DrawingArea widget, we are <em>completely</em> responsible for drawing the contents. If our window is obscured then uncovered, we get an exposure event and must redraw what was previously hidden.</p>
<p>Having to remember everything that was drawn on the screen so we can properly redraw it can, to say the least, be a nuisance. In addition, it can be visually distracting if portions of the window are cleared, then redrawn step by step. The solution to this problem is to use an offscreen <em>backing pixmap</em>. Instead of drawing directly to the screen, we draw to an image stored in server memory but not displayed, then when the image changes or new portions of the image are displayed, we copy the relevant portions onto the screen.</p>
<p>To create an offscreen pixmap, we call the function:</p>
<pre class="PROGRAMLISTING"><code>val GDraw.pixmap :
    width:int -&gt;
    height:int -&gt;
    ?mask:bool -&gt;
    ?window:&lt; misc : #misc_ops; .. &gt; -&gt;
    ?colormap:Gdk.colormap -&gt;
    unit -&gt; pixmap</code></pre>
<p>The <code class="LITERAL">window</code> parameter specifies a GDK window that this pixmap takes some of its properties from. <code class="LITERAL">width</code> and <code class="LITERAL">height</code> specify the size of the pixmap. <code class="LITERAL">colormap</code> tells the <em>color depth</em>, that is the number of bits per pixel, for the new window. If the colormap is not specified, <code class="LITERAL">default_colormap()</code> is used.</p>
<p>We create the pixmap in our &quot;configure&quot; handler. This event is generated whenever the window changes size, including when it is originally created.</p>
<pre class="PROGRAMLISTING"><code>(* Backing pixmap for drawing area *)
let backing = ref (GDraw.pixmap ~width:200 ~height:200 ())

(* Create a new backing pixmap of the appropriate size *)
let configure window backing ev =
  let width = GdkEvent.Configure.width ev in
  let height = GdkEvent.Configure.height ev in
  let pixmap = GDraw.pixmap ~width ~height ~window () in
  pixmap#set_foreground `WHITE;
  pixmap#rectangle ~x:0 ~y:0 ~width ~height ~filled:true ();
  backing := pixmap;
  true</code></pre>
<p>The call to <code class="LITERAL">rectangle</code> method clears the pixmap initially to white. We'll say more about that in a moment.</p>
<p>Our exposure event handler then simply copies the relevant portion of the pixmap onto the screen (we determine the area we need to redraw by using GdkEvent.Expost.area method to the exposure event):</p>
<pre class="PROGRAMLISTING"><code>(* Redraw the screen from the backing pixmap *)
let expose (drawing_area:GMisc.drawing_area) (backing:GDraw.pixmap ref) ev =
  let area = GdkEvent.Expose.area ev in
  let x = Gdk.Rectangle.x area in
  let y = Gdk.Rectangle.y area in
  let width = Gdk.Rectangle.width area in
  let height = Gdk.Rectangle.width area in
  let drawing =
    drawing_area#misc#realize ();
    new GDraw.drawable (drawing_area#misc#window)
  in
  drawing#put_pixmap ~x ~y ~xsrc:x ~ysrc:y ~width ~height !backing#pixmap;
  false</code></pre>
<p>We've now seen how to keep the screen up to date with our pixmap, but how do we actually draw interesting stuff on our pixmap? There are a large number of calls in GTK's GDK library for drawing on <em>drawables</em>. A drawable is simply something that can be drawn upon. It can be a window, a pixmap, or a bitmap (a black and white image). We've already seen two such calls above, <code class="LITERAL">rectangle</code> and <code class="LITERAL">put_pixmap</code> methods. The some of them are: see <a href="http://lablgtk.forge.ocamlcore.org/refdoc/GDraw.drawable-c.html"><code class="LITERAL">GDraw.drawable</code></a></p>
<pre class="PROGRAMLISTING"><code>method arc :
    x:int -&gt;
    y:int -&gt;
    width:int -&gt;
    height:int -&gt;
    ?filled:bool -&gt;
    ?start:float -&gt;
    ?angle:float -&gt;
    unit -&gt; unit
method line :
    x:int -&gt;
    y:int -&gt;
    x:int -&gt;
    y:int -&gt; unit
method point :
    x:int -&gt;
    y:int -&gt; unit
method polygon :
    ?filled:bool -&gt;
    (int * int) list -&gt; unit
method rectangle :
    x:int -&gt;
    y:int -&gt;
    width:int -&gt;
    height:int -&gt;
    ?filled:bool -&gt;
    unit -&gt; unit
method string :
    string -&gt;
    font:Gdk.font -&gt;
    x:int -&gt;
    y:int -&gt; unit
method points : (int * int) list -&gt; unit
method lines : (int * int) list -&gt; unit
method segments : ((int * int) * (int * int)) list -&gt; unit

method put_layout :
    x:int -&gt;
    y:int -&gt;
    ?fore:color -&gt;
    ?back:color -&gt;
    Pango.layout -&gt; unit
method put_image :
    x:int -&gt;
    y:int -&gt;
    ?xsrc:int -&gt;
    ?ysrc:int -&gt;
    ?width:int -&gt;
    ?height:int -&gt;
    Gdk.image -&gt; unit
method put_pixmap :
    x:int -&gt;
    y:int -&gt;
    ?xsrc:int -&gt;
    ?ysrc:int -&gt;
    ?width:int -&gt;
    ?height:int -&gt;
    Gdk.pixmap -&gt; unit
method put_rgb_data :
    width:int -&gt;
    height:int -&gt;
    ?x:int -&gt;
    ?y:int -&gt;
    ?dither:Gdk.Tags.rgb_dither -&gt;
    ?row_stride:int -&gt;
    Gpointer.region -&gt; unit
method put_pixbuf :
    x:int -&gt;
    y:int -&gt;
    ?width:int -&gt;
    ?height:int -&gt;
    ?dither:Gdk.Tags.rgb_dither -&gt;
    ?x_dither:int -&gt;
    ?y_dither:int -&gt;
    ?src_x:int -&gt;
    ?src_y:int -&gt;
    GdkPixbuf.pixbuf -&gt; unit</code></pre>
<p>All of these functions uses <em>graphics context</em> (GC). A graphics context encapsulates information about things such as foreground and background color and line width. GDK has a full set of functions for creating and modifying graphics contexts. <code class="LITERAL">GDraw.drawable</code> has the default GC and you can change it using:</p>
<pre class="PROGRAMLISTING"><code>method set_background : color -&gt; unit
method set_foreground : color -&gt; unit
method set_clip_region : Gdk.region -&gt; unit
method set_clip_origin : x:int -&gt; y:int -&gt; unit
method set_clip_mask : Gdk.bitmap -&gt; unit
method set_clip_rectangle : Gdk.Rectangle.t -&gt; unit
method set_line_attributes :
    ?width:int -&gt;
    ?style:Gdk.GC.gdkLineStyle -&gt;
    ?cap:Gdk.GC.gdkCapStyle -&gt;
    ?join:Gdk.GC.gdkJoinStyle -&gt;
    unit -&gt; unit</code></pre>
<p>Our function <code class="LITERAL">draw_brush</code>, which does the actual drawing on the screen, is then:</p>
<pre class="PROGRAMLISTING"><code>(* Draw a rectangle on the screen *)
let draw_brush (area:GMisc.drawing_area) (backing:GDraw.pixmap ref) x y =
  let x = x - 5 in
  let y = y - 5 in
  let width = 10 in
  let height = 10 in
  let update_rect = Gdk.Rectangle.create ~x ~y ~width ~height in
  !backing#set_foreground `BLACK;
  !backing#rectangle ~x ~y ~width ~height ~filled:true ();
  area#misc#draw (Some update_rect)</code></pre>
<p>After we draw the rectangle representing the brush onto the pixmap, we call the method:</p>
<pre class="PROGRAMLISTING"><code>method misc#draw : Gdk.Rectangle.t option -&gt; unit</code></pre>
<p>which notifies X that the given area needs to be updated. X will eventually generate an expose event (possibly combining the areas passed in several calls to <code class="LITERAL">misc#draw</code>) which will cause our expose event handler to copy the relevant portions to the screen.</p>
<p>We have now covered the entire drawing program except for a few mundane details like creating the main window.</p>
<hr />
<table>
<tbody>
<tr class="odd">
<td align="left"><a href="x2205.html">&lt;&lt;&lt; Previous</a></td>
<td align="left"><a href="book1.html">Home</a></td>
<td align="left"><a href="c2294.html">Next &gt;&gt;&gt;</a></td>
</tr>
<tr class="even">
<td align="left">Event Handling</td>
<td align="left"><a href="c2196.html">Up</a></td>
<td align="left">Contributing</td>
</tr>
</tbody>
</table>
