<table>
<thead>
<tr class="header">
<th align="left">GTK+ 2.0 Tutorial using Ocaml</th>
</tr>
</thead>
<tbody>
<tr class="odd">
<td align="left"><a href="x1135.html">&lt;&lt;&lt; Previous</a></td>
<td align="left">Miscellaneous Widgets</td>
<td align="left"><a href="x1183.html">Next &gt;&gt;&gt;</a></td>
</tr>
</tbody>
</table>
<hr />
<h1 class="SECT1">Text Entries</h1>
<p>The Entry widget allows text to be typed and displayed in a single line text box. The text may be set with function calls that allow new text to replace, prepend or append the current contents of the Entry widget.</p>
<p>Create a new Entry widget with the function <a href="http://lablgtk.forge.ocamlcore.org/refdoc/GEdit.html#VALentry"><code class="LITERAL">GEdit.entry</code></a>.</p>
<pre class="PROGRAMLISTING"><code>val GEdit.entry :
    ?text:string -&gt;
    ?visibility:bool -&gt;
    ?max_length:int -&gt;
    ?activates_default:bool -&gt;
    ?editable:bool -&gt;
    ?has_frame:bool -&gt;
    ?width_chars:int -&gt;
    ?width:int -&gt;
    ?height:int -&gt;
    ?packing:(GObj.widget -&gt; unit) -&gt;
    ?show:bool -&gt; unit -&gt; entry</code></pre>
<p>The next method alters the text which is currently within the Entry widget.</p>
<pre class="PROGRAMLISTING"><code>method set_text : string -&gt; unit</code></pre>
<p>The set_text method sets the contents of the Entry widget, replacing the current contents. Note that the class Entry implements the Editable interface (yes, gobject supports Java-like interfaces) which contains some more functions for manipulating the contents.</p>
<p>The contents of the Entry can be retrieved by using a call to the following method. This is useful in the callback functions described below.</p>
<pre class="PROGRAMLISTING"><code>method text : string</code></pre>
<p>If we don't want the contents of the Entry to be changed by someone typing into it, we can change its editable state.</p>
<pre class="PROGRAMLISTING"><code>method set_editable : bool -&gt; unit</code></pre>
<p>The method above allows us to toggle the editable state of the Entry widget by passing in a true or false value as argument.</p>
<p>If we are using the Entry where we don't want the text entered to be visible, for example when a password is being entered, we can use the following method, which also takes a boolean flag.</p>
<pre class="PROGRAMLISTING"><code>method set_visibility : bool -&gt; unit</code></pre>
<p>A region of the text may be set as selected by using the following method. This would most often be used after setting some default text in an Entry, making it easy for the user to remove it.</p>
<pre class="PROGRAMLISTING"><code>method select_region : start:int -&gt; stop:int -&gt; unit</code></pre>
<p>If we want to catch when the user has entered text, we can connect to the <code class="LITERAL">activate</code> or <code class="LITERAL">changed</code> signal. Activate is raised when the user hits the enter key within the Entry widget. Changed is raised when the text changes at all, e.g., for every character entered or removed.</p>
<p>The following code is an example of using an Entry widget.</p>
<p><img src="images/entry.png" /></p>
<pre class="PROGRAMLISTING"><code>(* file: entry.ml *)

let enter_cb entry () =
  let text = entry#text in
  Printf.printf &quot;Entry contents: %s\n&quot; text;
  flush stdout

let toggle checkbutton f () = f checkbutton#active

let main () =
  (* Create a new window; set title and border width *)
  let window = GWindow.window ~title:&quot;Entry&quot; 
    ~width:200 ~height:100 ~border_width:10 () in

  (* Set a handler for destroy event that immediately exits GTK. *)
  window#connect#destroy ~callback:GMain.Main.quit;

  let vbox = GPack.vbox ~packing:window#add () in

  let entry = GEdit.entry ~text:&quot;hello&quot; ~max_length:500 ~packing:vbox#add () in
  entry#connect#activate ~callback:(enter_cb entry);
  let tmp_pos = entry#text_length in
  entry#insert_text &quot; world&quot; tmp_pos;
  entry#select_region ~start:0 ~stop:entry#text_length;

  let hbox = GPack.hbox ~packing:vbox#add () in
  let check = GButton.check_button ~label:&quot;Editable&quot;
    ~active:true ~packing:hbox#add () in
  check#connect#toggled ~callback:(toggle check entry#set_editable);

  let check = GButton.check_button ~label:&quot;Visible&quot;
    ~active:true ~packing:hbox#add () in
  check#connect#toggled ~callback:(toggle check entry#set_visibility);

  let button = GButton.button ~stock:`CLOSE ~packing:vbox#add () in
  button#connect#clicked ~callback:window#destroy;

  button#misc#set_can_default true;
  button#misc#grab_default ();

  window#show ();
  GMain.Main.main ()

let _ = Printexc.print main ()</code></pre>
<hr />
<table>
<tbody>
<tr class="odd">
<td align="left"><a href="x1135.html">&lt;&lt;&lt; Previous</a></td>
<td align="left"><a href="book1.html">Home</a></td>
<td align="left"><a href="x1183.html">Next &gt;&gt;&gt;</a></td>
</tr>
<tr class="even">
<td align="left">Statusbars</td>
<td align="left"><a href="c953.html">Up</a></td>
<td align="left">Spin Buttons</td>
</tr>
</tbody>
</table>
