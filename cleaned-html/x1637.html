<table>
<thead>
<tr class="header">
<th align="left">GTK+ 2.0 Tutorial using Ocaml</th>
</tr>
</thead>
<tbody>
<tr class="odd">
<td align="left"><a href="x1623.html">&lt;&lt;&lt; Previous</a></td>
<td align="left">Container Widgets</td>
<td align="left"><a href="x1684.html">Next &gt;&gt;&gt;</a></td>
</tr>
</tbody>
</table>
<hr />
<h1 class="SECT1">Toolbar</h1>
<p>Toolbars are usually used to group some number of widgets in order to simplify customization of their look and layout. Typically a toolbar consists of buttons with icons, labels and tooltips, but any other widget can also be put inside a toolbar. Finally, items can be arranged horizontally or vertically and buttons can be displayed with icons, labels, or both.</p>
<p>Creating a toolbar is (as one may already suspect) done with the function <a href="http://lablgtk.forge.ocamlcore.org/refdoc/GButton.html#VALtoolbar"><code class="LITERAL">GButton.toolbar</code></a>:</p>
<pre class="PROGRAMLISTING"><code>val GButton.toolbar :
    ?orientation:Gtk.Tags.orientation -&gt;
    ?style:Gtk.Tags.toolbar_style -&gt;
    ?tooltips:bool -&gt;
    ?border_width:int -&gt;
    ?width:int -&gt;
    ?height:int -&gt;
    ?packing:(GObj.widget -&gt; unit) -&gt;
    ?show:bool -&gt; unit -&gt; toolbar</code></pre>
<p>After creating a toolbar one can insert button, radio_button, toggle_button or any widget types into the toolbar. To describe an item we need a label text, a tooltip text, a private tooltip text, an icon for the button and a callback function for it. For example, to insert an item you may use the following functions:</p>
<pre class="PROGRAMLISTING"><code>method insert_button :
    ?text:string -&gt;
    ?tooltip:string -&gt;
    ?tooltip_private:string -&gt;
    ?icon:GObj.widget -&gt;
    ?pos:int -&gt;
    ?callback:(unit -&gt; unit) -&gt; unit -&gt; button

method insert_radio_button :
    ?text:string -&gt;
    ?tooltip:string -&gt;
    ?tooltip_private:string -&gt;
    ?icon:GObj.widget -&gt;
    ?pos:int -&gt;
    ?callback:(unit -&gt; unit) -&gt; unit -&gt; radio_button

method insert_toggle_button :
    ?text:string -&gt;
    ?tooltip:string -&gt;
    ?tooltip_private:string -&gt;
    ?icon:GObj.widget -&gt;
    ?pos:int -&gt;
    ?callback:(unit -&gt; unit) -&gt; unit -&gt; toggle_button

method insert_widget :
    ?tooltip:string -&gt;
    ?tooltip_private:string -&gt;
    ?pos:int -&gt;
    GObj.widget -&gt; unit

pos: default value is (-1)</code></pre>
<p>If <code class="LITERAL">pos</code> is 0 the item is prepended to the start of the toolbar. If <code class="LITERAL">pos</code> is negative, the item is appended to the end of the toolbar. It's default value is -1.</p>
<p>To simplify adding spaces between toolbar items, you may use the following function:</p>
<pre class="PROGRAMLISTING"><code>method insert_space : ?pos:int -&gt; unit -&gt; unit</code></pre>
<p>If it's required, the orientation of a toolbar and its style can be changed &quot;on the fly&quot; using the following functions:</p>
<pre class="PROGRAMLISTING"><code>method set_orientation : Gtk.Tags.orientation -&gt; unit
method set_style : Gtk.Tags.toolbar_style -&gt; unit
method set_tooltips : bool -&gt; unit</code></pre>
<p>Where <code class="LITERAL">orientation</code> is one of <code class="LITERAL">`HORIZONTAL</code> or <code class="LITERAL">`VERTICAL</code>. The <code class="LITERAL">style</code> is used to set appearance of the toolbar items by using one of <code class="LITERAL">`ICONS</code>, <code class="LITERAL">`TEXT</code>, or <code class="LITERAL">`BOTH</code>.</p>
<p>To show some other things that can be done with a toolbar, let's take the following program (we'll interrupt the listing with some additional explanations):</p>
<pre class="PROGRAMLISTING"><code>(* file: toolbar.ml *)

(* that&#39;s easy... when one of the buttons is toggled,
 * set the style of the toolbar accordingly *)
let radio_event toolbar style () =
  toolbar#set_style style

(* just check given toggle button and enable/disable
 * tooltips *)
let toggle_event toolbar button () =
  toolbar#set_tooltips button#active</code></pre>
<p>The above are just two callback functions that will be called when one of the buttons on a toolbar is pressed. You should already be familiar with things like this if you've already used toggle buttons (and radio buttons).</p>
<pre class="PROGRAMLISTING"><code>let main () =
  (* Create a new window with a given title, and nice size *)
  let dialog = GWindow.dialog ~title:&quot;Toolbar Tutorial&quot; () in

  (* typically we quit if someone tries to close us *)
  dialog#connect#destroy ~callback:GMain.Main.quit;

  (* we neecd to realize the window because we use pixmaps for
   * items on the toolbar in the context of it *)
  dialog#misc#realize ();

  (* to make it nice we&#39;ll put the toolbar into the handle box,
   * so that it can be detached from the main window *)
  let handlebox = GBin.handle_box ~packing:dialog#vbox#add () in</code></pre>
<p>The above should be similar to any other GTK application. Just initialization of GTK, creating the window, etc. There is only one thing that probably needs some explanation: a handle box. A handle box is just another box that can be used to pack widgets in to. The difference between it and typical boxes is that it can be detached from a parent window (or, in fact, the handle box remains in the parent, but it is reduced to a very small rectangle, while all of its contents are reparented to a new freely floating window). It is usually nice to have a detachable toolbar, so these two widgets occur together quite often.</p>
<pre class="PROGRAMLISTING"><code>  (* toolbar will be horizontal, with both icons and text, and
   * with 5pxl spaces between items and finally,
   * we&#39;ll also put it into our handlebox *)
  let toolbar = GButton.toolbar
    ~orientation:`HORIZONTAL
    ~style:`BOTH
    ~border_width:5 (* ~space_size:5 *)
    ~packing:handlebox#add () in

  (* we need icon for toolbar buttons *)
  let icon () =
    let info = GDraw.pixmap_from_xpm ~file:&quot;gtk.xpm&quot; () in
    (GMisc.pixmap info ())#coerce
  in</code></pre>
<p>Well, what we do above is just a straightforward initialization of the toolbar widget.</p>
<pre class="PROGRAMLISTING"><code>  (* our first item is &quot;close&quot; button *)
  let button = toolbar#insert_button
    ~text:&quot;Close&quot;
    ~tooltip:&quot;Close this app&quot;
    ~tooltip_private:&quot;Private&quot;
    ~icon:(icon ())
    ~callback:GMain.Main.quit () in
  toolbar#insert_space (); (* space after item *)</code></pre>
<p>In the above code you see the simplest case: adding a button to toolbar. Just before appending a new item, we have to construct an image widget to serve as an icon for this item; this step will have to be repeated for each new item. Just after the item we also add a space, so the following items will not touch each other. As you see toolbar#insert_button returns a our newly created button widget, so that we can work with it in the normal way.</p>
<pre class="PROGRAMLISTING"><code>  (* now, lets make our radio buttons group... *)
  let icon_button = toolbar#insert_radio_button
    ~text:&quot;Icon&quot;
    ~tooltip:&quot;Only icons in toolbar&quot;
    ~tooltip_private:&quot;Private&quot;
    ~icon:(icon ())
    ~callback:(radio_event toolbar `ICONS) () in
  toolbar#insert_space ();</code></pre>
<p>Here we begin creating a radio buttons group. To do this we use toolbar#insert_radio_button. In the above case we start creating a radio group. In creating other radio buttons for this group the previous button in the group is required, so that a list of buttons can be easily constructed (see the section on <a href="x743.html">Radio Buttons</a> earlier in this tutorial).</p>
<pre class="PROGRAMLISTING"><code>  (* following radio buttons refer to previous ones *)
  let text_button = toolbar#insert_radio_button
    ~text:&quot;Text&quot;
    ~tooltip:&quot;Only texts in toolbar&quot;
    ~tooltip_private:&quot;Private&quot;
    ~icon:(icon ())
    ~callback:(radio_event toolbar `TEXT) () in
  text_button#set_group icon_button#group;
  toolbar#insert_space ();

  let both_button = toolbar#insert_radio_button
    ~text:&quot;Both&quot;
    ~tooltip:&quot;Icons and text in toolbar&quot;
    ~tooltip_private:&quot;Private&quot;
    ~icon:(icon ())
    ~callback:(radio_event toolbar `BOTH) () in
  both_button#set_group text_button#group;
  both_button#set_active true;
  toolbar#insert_space ();</code></pre>
<p>In the end we have to set the state of one of the buttons manually.</p>
<pre class="PROGRAMLISTING"><code>  (* here we have just a simple toggle button *)
  let tooltip_button = toolbar#insert_toggle_button
    ~text:&quot;Tooltips&quot;
    ~tooltip:&quot;Toolbar with or without tips&quot;
    ~tooltip_private:&quot;Private&quot;
    ~icon:(icon ()) () in
  tooltip_button#connect#clicked ~callback:(toggle_event toolbar tooltip_button);
  tooltip_button#set_active true;
  toolbar#insert_space ();</code></pre>
<p>A toggle button can be created in the obvious way (if one knows how to create radio buttons already).</p>
<pre class="PROGRAMLISTING"><code>  (* to pack a widget into toolbar, we only have to
   * create it and append it with appropriate tooltip *)
  let entry = GEdit.entry () in
  toolbar#insert_widget 
    ~tooltip:&quot;This is just an entry&quot;
    ~tooltip_private:&quot;Private&quot;
    entry#coerce;</code></pre>
<p>As you see, adding any kind of widget to a toolbar is simple.</p>
<pre class="PROGRAMLISTING"><code>  (* that&#39;s it! let&#39;s show everything *)
  dialog#show ();
  (* rest in GMain.Main.main () and wait for the fun to begin! *)
  GMain.Main.main ()

let _ = Printexc.print main ()</code></pre>
<p>So, here we are at the end of toolbar tutorial. Of course, to appreciate it in full you need also this nice XPM icon, so here it is:</p>
<p></p>
<p><img src="./stylesheet-images/note.gif" alt="Note" /></p>
<p>The following xpm file is c style but it's ok in ocaml too.</p>
<pre class="PROGRAMLISTING"><code>/* XPM */
static char * gtk_xpm[] = {
&quot;32 39 5 1&quot;,
&quot;.      c none&quot;,
&quot;+      c black&quot;,
&quot;@      c #3070E0&quot;,
&quot;#      c #F05050&quot;,
&quot;$      c #35E035&quot;,
&quot;................+...............&quot;,
&quot;..............+++++.............&quot;,
&quot;............+++++@@++...........&quot;,
&quot;..........+++++@@@@@@++.........&quot;,
&quot;........++++@@@@@@@@@@++........&quot;,
&quot;......++++@@++++++++@@@++.......&quot;,
&quot;.....+++@@@+++++++++++@@@++.....&quot;,
&quot;...+++@@@@+++@@@@@@++++@@@@+....&quot;,
&quot;..+++@@@@+++@@@@@@@@+++@@@@@++..&quot;,
&quot;.++@@@@@@+++@@@@@@@@@@@@@@@@@@++&quot;,
&quot;.+#+@@@@@@++@@@@+++@@@@@@@@@@@@+&quot;,
&quot;.+##++@@@@+++@@@+++++@@@@@@@@$@.&quot;,
&quot;.+###++@@@@+++@@@+++@@@@@++$$$@.&quot;,
&quot;.+####+++@@@+++++++@@@@@+@$$$$@.&quot;,
&quot;.+#####+++@@@@+++@@@@++@$$$$$$+.&quot;,
&quot;.+######++++@@@@@@@++@$$$$$$$$+.&quot;,
&quot;.+#######+##+@@@@+++$$$$$$@@$$+.&quot;,
&quot;.+###+++##+##+@@++@$$$$$$++$$$+.&quot;,
&quot;.+###++++##+##+@@$$$$$$$@+@$$@+.&quot;,
&quot;.+###++++++#+++@$$@+@$$@++$$$@+.&quot;,
&quot;.+####+++++++#++$$@+@$$++$$$$+..&quot;,
&quot;.++####++++++#++$$@+@$++@$$$$+..&quot;,
&quot;.+#####+++++##++$$++@+++$$$$$+..&quot;,
&quot;.++####+++##+#++$$+++++@$$$$$+..&quot;,
&quot;.++####+++####++$$++++++@$$$@+..&quot;,
&quot;.+#####++#####++$$+++@++++@$@+..&quot;,
&quot;.+#####++#####++$$++@$$@+++$@@..&quot;,
&quot;.++####++#####++$$++$$$$$+@$@++.&quot;,
&quot;.++####++#####++$$++$$$$$$$$+++.&quot;,
&quot;.+++####+#####++$$++$$$$$$$@+++.&quot;,
&quot;..+++#########+@$$+@$$$$$$+++...&quot;,
&quot;...+++########+@$$$$$$$$@+++....&quot;,
&quot;.....+++######+@$$$$$$$+++......&quot;,
&quot;......+++#####+@$$$$$@++........&quot;,
&quot;.......+++####+@$$$$+++.........&quot;,
&quot;.........++###+$$$@++...........&quot;,
&quot;..........++##+$@+++............&quot;,
&quot;...........+++++++..............&quot;,
&quot;.............++++...............&quot;};</code></pre>
<hr />
<table>
<tbody>
<tr class="odd">
<td align="left"><a href="x1623.html">&lt;&lt;&lt; Previous</a></td>
<td align="left"><a href="book1.html">Home</a></td>
<td align="left"><a href="x1684.html">Next &gt;&gt;&gt;</a></td>
</tr>
<tr class="even">
<td align="left">Button Boxes</td>
<td align="left"><a href="c1436.html">Up</a></td>
<td align="left">Notebooks</td>
</tr>
</tbody>
</table>
