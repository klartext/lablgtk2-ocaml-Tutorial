<table>
<thead>
<tr class="header">
<th align="left">GTK+ 2.0 Tutorial using Ocaml</th>
</tr>
</thead>
<tbody>
<tr class="odd">
<td align="left"><a href="x1684.html">&lt;&lt;&lt; Previous</a></td>
<td align="left"></td>
<td align="left"><a href="x1823.html">Next &gt;&gt;&gt;</a></td>
</tr>
</tbody>
</table>
<hr />
<h1>Menu Widget</h1>
<p>There are two ways to create menus: there's the easy way, and there's the hard way. Both have their uses, but you can usually use the Itemfactory (the easy way). The &quot;hard&quot; way is to create all the menus using the calls directly. The easy way is to use the gtk_item_factory calls. This is much simpler, but there are advantages and disadvantages to each approach.</p>
<p>The Itemfactory is much easier to use, and to add new menus to, although writing a few wrapper functions to create menus using the manual method could go a long way towards usability. With the Itemfactory, it is not possible to add images or the character '/' to the menus.</p>
<h1 class="SECT1">Manual Menu Creation</h1>
<p>In the true tradition of teaching, we'll show you the hard way first. <code class="LITERAL">:)</code></p>
<p>There are three widgets that go into making a menubar and submenus:</p>
<p></p>
<ul>
<li><p>a menu item, which is what the user wants to select, e.g., &quot;Save&quot;</p></li>
<li><p>a menu, which acts as a container for the menu items, and</p></li>
<li><p>a menubar, which is a container for each of the individual menus.</p></li>
</ul>
<p>This is slightly complicated by the fact that menu item widgets are used for two different things. They are both the widgets that are packed into the menu, and the widget that is packed into the menubar, which, when selected, activates the menu.</p>
<p>Let's look at the functions that are used to create menus and menubars. This first function <a href="http://lablgtk.forge.ocamlcore.org/refdoc/GMenu.html#VALmenu_bar"><code class="LITERAL">GMenu.menu_bar</code></a> is used to create a new menubar.</p>
<pre class="PROGRAMLISTING"><code>GMenu.menu_bar : ?border_width:int -&gt;
    ?width:int -&gt;
    ?height:int -&gt;
    ?packing:(GObj.widget -&gt; unit) -&gt;
    ?show:bool -&gt;
    unit -&gt; menu_shell</code></pre>
<p>This rather self explanatory function creates a new menubar. You use gtk_container_add() to pack this into a window, or the box_pack functions to pack it into a box - the same as buttons: <a href="http://lablgtk.forge.ocamlcore.org/refdoc/GMenu.html#VALmenu"><code class="LITERAL">GMenu.menu</code></a>.</p>
<pre class="PROGRAMLISTING"><code>GMenu.menu : ?accel_path:string -&gt;
    ?border_width:int -&gt;
    ?packing:(menu -&gt; unit) -&gt;
    ?show:bool -&gt;
    unit -&gt; menu</code></pre>
<p>This function returns a new menu; it is never actually shown, it is just a container for the menu items. I hope this will become more clear when you look at the example below.</p>
<p>The next call <a href="http://lablgtk.forge.ocamlcore.org/refdoc/GMenu.html#VALmenu_item"><code class="LITERAL">GMenu.menu_item</code></a> is used to create menu item that is packed into the menu (and menubar).</p>
<pre class="PROGRAMLISTING"><code>GMenu.menu_item : ?use_mnemonic:bool -&gt;
    ?label:string -&gt;
    ?right_justified:bool -&gt;
    ?packing:(menu_item -&gt; unit) -&gt;
    ?show:bool -&gt;
    unit -&gt; menu_item</code></pre>
<p>This call is used to create a menu item that is to be displayed. Remember to differentiate between a &quot;menu&quot; as created with GMenu.menu () and a &quot;menu item&quot; as created by the GMenu.menu_item () functions. The menu item will be an actual button with an associated action, whereas a menu will be a container holding menu items.</p>
<p>The ~label option of <a href="http://lablgtk.forge.ocamlcore.org/refdoc/GMenu.html#VALmenu_item"><code class="LITERAL">GMenu.menu_item</code></a> creates a new menu item with a label already packed into it.</p>
<p>Once you've created a menu item you have to put it into a menu. This is done using the function <a href="http://lablgtk.forge.ocamlcore.org/refdoc/GMenu.menu-c.html#METHODappend"><code class="LITERAL">GMenu.menu#append</code></a>. In order to capture when the item is selected by the user, we need to connect to the <code class="LITERAL">activate</code> signal in the usual way. So, if we wanted to create a standard <code class="LITERAL">File</code> menu, with the options <code class="LITERAL">Open</code>, <code class="LITERAL">Save</code>, and <code class="LITERAL">Quit</code>, the code would look something like:</p>
<pre class="PROGRAMLISTING"><code>let create_menu () =
  let file_menu = GMenu.menu () in
  let item = GMenu.menu_item ~label:&quot;Open&quot; ~packing:file_menu#append () in
  item#connect#activate ~callback:(uprint &quot;Open&quot;);
  let item = GMenu.menu_item ~label:&quot;Save&quot; ~packing:file_menu#append () in
  item#connect#activate ~callback:(uprint &quot;Save&quot;);
  let item = GMenu.menu_item ~label:&quot;Quit&quot; ~packing:file_menu#append () in
  item#connect#activate ~callback:GMain.Main.quit;
  file_menu</code></pre>
<p>At this point we have our menu. Now we need to create a menubar and a menu item for the <code class="LITERAL">File</code> entry, to which we add our menu. The code looks like this:</p>
<pre class="PROGRAMLISTING"><code>  let file_menu = create_menu () in
  let menu_bar = GMenu.menu_bar ~packing:window#add () in
  let file_item = GMenu.menu_item ~label:&quot;File&quot; () in</code></pre>
<p>Now we need to associate the menu with <code class="LITERAL">file_item</code>. This is done with the function</p>
<pre class="PROGRAMLISTING"><code>GMenu.menu_item#set_submenu : menu -&gt; unit</code></pre>
<p>So, our example would continue with</p>
<pre class="PROGRAMLISTING"><code>  file_item#set_submenu file_menu;</code></pre>
<p>All that is left to do is to add the menu to the menubar, which is accomplished using the function</p>
<pre class="PROGRAMLISTING"><code>GMenu.menu_shell#append : GMenu.menu_item -&gt; unit</code></pre>
<p>which in our case looks like this:</p>
<pre class="PROGRAMLISTING"><code>  menu_bar#append file_item;</code></pre>
<p>The complete code looks like this:</p>
<pre class="PROGRAMLISTING"><code>let uprint msg () =
  print_endline msg;
  flush stdout

let create_menu () =
  let file_menu = GMenu.menu () in
  let item = GMenu.menu_item ~label:&quot;Open&quot; ~packing:file_menu#append () in
  item#connect#activate ~callback:(uprint &quot;Open&quot;);
  let item = GMenu.menu_item ~label:&quot;Save&quot; ~packing:file_menu#append () in
  item#connect#activate ~callback:(uprint &quot;Save&quot;);
  let item = GMenu.menu_item ~label:&quot;Quit&quot; ~packing:file_menu#append () in
  item#connect#activate ~callback:GMain.Main.quit;
  file_menu

let main () =
  let window = GWindow.window () in
  window#connect#destroy ~callback:GMain.Main.quit;
  let file_menu = create_menu () in
  let menu_bar = GMenu.menu_bar ~packing:window#add () in
  let file_item = GMenu.menu_item ~label:&quot;File&quot; () in
  file_item#set_submenu file_menu;
  menu_bar#append file_item;
  window#show ();
  GMain.Main.main ()

let _ = main ()</code></pre>
<p>The more compact code looks like this:</p>
<pre class="PROGRAMLISTING"><code>let uprint msg () =
  print_endline msg;
  flush stdout

let create_menu ~packing () =
  let file_menu = GMenu.menu ~packing () in
  let item = GMenu.menu_item ~label:&quot;Open&quot; ~packing:file_menu#append () in
  item#connect#activate ~callback:(uprint &quot;Open&quot;);
  let item = GMenu.menu_item ~label:&quot;Save&quot; ~packing:file_menu#append () in
  item#connect#activate ~callback:(uprint &quot;Save&quot;);
  let item = GMenu.menu_item ~label:&quot;Quit&quot; ~packing:file_menu#append () in
  item#connect#activate ~callback:GMain.Main.quit

let main () =
  let window = GWindow.window () in
  window#connect#destroy ~callback:GMain.Main.quit;
  let menu_bar = GMenu.menu_bar ~packing:window#add () in
  let file_item = GMenu.menu_item ~label:&quot;File&quot; ~packing:menu_bar#append () in
  create_menu ~packing:file_item#set_submenu ();
  window#show ();
  GMain.Main.main ()

let _ = main ()</code></pre>
<p>If we wanted the menu right justified on the menubar, such as help menus often are, we can use the ~right_justify option (again on <code class="LITERAL">file_item</code> in the current example) on menu_item createion.</p>
<pre class="PROGRAMLISTING"><code>  let item = GMenu.menu_item ~right_justify:true () in
  ...</code></pre>
<p>Here is a summary of the top down steps needed to create a menu bar with menus attached:</p>
<p></p>
<ul>
<li><p>Create a new menubar using GMenu.menu_bar (). This step only needs to be done once when creating a series of menus on one menu bar.</p></li>
<li><p>Create a menu item using GMenu.menu_item (). This will be the root of the menu, the text appearing here will be on the menubar itself.</p>
<p>Use GMenu.menu_bar#append method to put the root menu item onto the menubar.</p></li>
<li><p>Create a new menu using GMenu.menu ()</p>
<p>Use GMenu.menu_item#set_submenu method to attach the menu to the root menu item (the one created in the above step).</p></li>
<li><p>Use multiple calls to GMenu.menu_item () for each item you wish to have on your menu. And use GMenu.menu#append method to put each of these new items on to the menu.</p></li>
</ul>
<p>Creating a popup menu is nearly the same. The difference is that the menu is not posted &quot;automatically&quot; by a menubar, but explicitly by calling the function GMenu.menu#popup method from a button-press event, for example. Take these steps:</p>
<p></p>
<ul>
<li><p>Create an event handling function. It needs to have the prototype</p>
<pre class="PROGRAMLISTING"><code>let button_pressed ev =
  ...</code></pre>
<p>But for poping up menu, you may give one more argument for the event handling function like this:</p>
<pre class="PROGRAMLISTING"><code>let button_pressed menu ev =
  ...
  menu#popup ~button ~time:(GdkEvent.Button.time ev);
  ...</code></pre>
<p>and it will use the event to find out where to pop up the menu.</p></li>
<li><p>In the event handler, if the event is a mouse button press, treat <code class="LITERAL">event</code> as a button event (which it is) and use it as shown in the sample code to pass information to GMenu.menu#popup method.</p></li>
<li><p>Bind that event handler to a widget with</p>
<pre class="PROGRAMLISTING"><code>  widget#event#connect#button_press ~callback:(button_pressed menu);</code></pre>
<p>where <code class="LITERAL">widget</code> is the widget you are binding to, <code class="LITERAL">handler</code> is the handling function, and <code class="LITERAL">menu</code> is a menu created with GMenu.menu (). This can be a menu which is also posted by a menu bar, as shown in the sample code.</p></li>
</ul>
<hr />
<table>
<tbody>
<tr class="odd">
<td align="left"><a href="x1684.html">&lt;&lt;&lt; Previous</a></td>
<td align="left"><a href="book1.html">Home</a></td>
<td align="left"><a href="x1823.html">Next &gt;&gt;&gt;</a></td>
</tr>
<tr class="even">
<td align="left">Notebooks</td>
<td align="left">Â </td>
<td align="left">Manual Menu Example</td>
</tr>
</tbody>
</table>
