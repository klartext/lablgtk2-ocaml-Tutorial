<table>
<thead>
<tr class="header">
<th align="left">GTK+ 2.0 Tutorial using Ocaml</th>
</tr>
</thead>
<tbody>
<tr class="odd">
<td align="left"><a href="x1103.html">&lt;&lt;&lt; Previous</a></td>
<td align="left">Miscellaneous Widgets</td>
<td align="left"><a href="x1155.html">Next &gt;&gt;&gt;</a></td>
</tr>
</tbody>
</table>
<hr />
<h1 class="SECT1">Statusbars</h1>
<p>Statusbars are simple widgets used to display a text message. They keep a stack of the messages pushed onto them, so that popping the current message will re-display the previous text message.</p>
<p>In order to allow different parts of an application to use the same statusbar to display messages, the statusbar widget issues Context Identifiers which are used to identify different &quot;users&quot;. The message on top of the stack is the one displayed, no matter what context it is in. Messages are stacked in last-in-first-out order, not context identifier order.</p>
<p>A statusbar is created with a call to <a href="http://lablgtk.forge.ocamlcore.org/refdoc/GMisc.html#VALstatus_bar"><code class="LITERAL">GMisc.status_bar</code></a> :</p>
<pre class="PROGRAMLISTING"><code>val GMisc.statusbar :
    ?border_width:int -&gt;
    ?width:int -&gt;
    ?height:int -&gt;
    ?packing:(GObj.widget -&gt; unit) -&gt;
    ?show:bool -&gt; unit -&gt; statusbar</code></pre>
<p>A new Context Identifier is requested using a call to the following function with a short textual description of the context:</p>
<pre class="PROGRAMLISTING"><code>method new_context : name:string -&gt; statusbar_context</code></pre>
<p>There are three functions that can operate on statusbar_contexts:</p>
<pre class="PROGRAMLISTING"><code>method push : string -&gt; Gtk.statusbar_message
method pop : unit -&gt; unit
method remove : Gtk.statusbar_message -&gt; unit</code></pre>
<p>The first, push method, is used to add a new message to the statusbar. It returns a Message Identifier, which can be passed later to the remove method to remove the message with the given Message Identifiers and Context from the statusbar's stack.</p>
<p>The method pop removes the message highest in the stack with the given Context.</p>
<p>The following example creates a statusbar and two buttons, one for pushing items onto the statusbar, and one for popping the last item back off.</p>
<p><img src="images/statusbar.png" /></p>
<pre class="PROGRAMLISTING"><code>(* file: statusbar.ml *)

let count = ref 0

let push_item context () =
  incr count;
  context#push (Printf.sprintf &quot;item %d&quot; !count);
  ()

let pop_item context () =
  context#pop ();
  ()

let main () =
  (* Create a new window; set title and border width *)
  let window = GWindow.window ~title:&quot;Statusbar&quot; () in

  (* Set a handler for destroy event that immediately exits GTK. *)
  window#connect#destroy ~callback:GMain.Main.quit;

  let vbox = GPack.vbox ~packing:window#add () in

  let statusbar = GMisc.statusbar ~packing:vbox#add () in
  let context = statusbar#new_context ~name:&quot;Statusbar example&quot; in

  let button = GButton.button ~label:&quot;push item&quot; ~packing:vbox#add () in
  button#connect#clicked ~callback:(push_item context);

  let button = GButton.button ~label:&quot;pop last item&quot; ~packing:vbox#add () in
  button#connect#clicked ~callback:(pop_item context);

  (* always display the window as the last step so it all splashes on
   * the screen at once. *)
  window#show ();
  GMain.Main.main ()

let _ = Printexc.print main ()</code></pre>
<hr />
<table>
<tbody>
<tr class="odd">
<td align="left"><a href="x1103.html">&lt;&lt;&lt; Previous</a></td>
<td align="left"><a href="book1.html">Home</a></td>
<td align="left"><a href="x1155.html">Next &gt;&gt;&gt;</a></td>
</tr>
<tr class="even">
<td align="left">Rulers</td>
<td align="left"><a href="c953.html">Up</a></td>
<td align="left">Text Entries</td>
</tr>
</tbody>
</table>
