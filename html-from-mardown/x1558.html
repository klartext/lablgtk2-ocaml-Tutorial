<table>
<thead>
<tr class="header">
<th align="left">GTK+ 2.0 Tutorial using Ocaml</th>
</tr>
</thead>
<tbody>
<tr class="odd">
<td align="left"><a href="x1538.html">&lt;&lt;&lt; Previous</a></td>
<td align="left">Container Widgets</td>
<td align="left"><a href="x1577.html">Next &gt;&gt;&gt;</a></td>
</tr>
</tbody>
</table>
<hr />
<h1 class="SECT1">Paned Window Widgets</h1>
<p>The paned window widgets are useful when you want to divide an area into two parts, with the relative size of the two parts controlled by the user. A groove is drawn between the two portions with a handle that the user can drag to change the ratio. The division can either be horizontal (`HORIZONTAL) or vertical (`VERTICAL).</p>
<p>To create a new paned window, call <a href="http://lablgtk.forge.ocamlcore.org/refdoc/GPack.html#VALpaned"><code class="LITERAL">GPack.paned</code></a> with orientation(`HORIZONTAL or `VERTICAL):</p>
<pre class="PROGRAMLISTING"><code>val GPack.paned :
    Gtk.Tags.orientation -&gt;
    ?border_width:int -&gt;
    ?width:int -&gt;
    ?height:int -&gt;
    ?packing:(GObj.widget -&gt; unit) -&gt;
    ?show:bool -&gt; unit -&gt; paned</code></pre>
<p>After creating the paned window widget, you need to add child widgets to its two halves. To do this, use the functions:</p>
<pre class="PROGRAMLISTING"><code>method add1 : GObj.widget -&gt; unit
method add2 : GObj.widget -&gt; unit</code></pre>
<p><code class="LITERAL">add1</code> method adds the child widget to the left or top half of the paned window. <code class="LITERAL">add2</code> method adds the child widget to the right or bottom half of the paned window.</p>
<p>As an example, we will create part of the user interface of an imaginary email program. A window is divided into two portions vertically, with the top portion being a list of email messages and the bottom portion the text of the email message. Most of the program is pretty straightforward. A couple of points to note: text can't be added to a Text widget until it is realized. This could be done by calling #misc#realize method, but as a demonstration of an alternate technique, we connect a handler to the &quot;realize&quot; signal to add the text. Also, we need to add the <code class="LITERAL">GTK_SHRINK</code> option to some of the items in the table containing the text window and its scrollbars, so that when the bottom portion is made smaller, the correct portions shrink instead of being pushed off the bottom of the window.</p>
<p><img src="images/paned.png" /></p>
<pre class="PROGRAMLISTING"><code>(* file: paned.ml *)

let cols = new GTree.column_list
let str_col = cols#add Gobject.Data.string

(* Create the list of &quot;messages&quot; *)
let create_list () =
  (* Create a new scrolled window, with scrollbars only if needed *)
  let scrolled_window = GBin.scrolled_window
    ~hpolicy:`AUTOMATIC ~vpolicy:`AUTOMATIC () in

  let model = GTree.list_store cols in
  let treeview = GTree.view ~model ~packing:(scrolled_window#add_with_viewport) () in

  for i = 0 to 10 do
    let iter = model#append () in
    model#set ~row:iter ~column:str_col (Printf.sprintf &quot;Message #%d&quot; i)
  done;
  let renderer = GTree.cell_renderer_text [] in
  let column = GTree.view_column ~title:&quot;Messages&quot;
    ~renderer:(renderer, [&quot;text&quot;, str_col]) () in
  treeview#append_column column;
  scrolled_window#coerce

(* Add some text to our text widget - this is a callback that is invoked
 * when our window is realized. We could also force our window to be
 * realized with #misc#realize, but it would have to be part of
 * a hierarchy first *)
let insert_text (buffer: GText.buffer) =
  let iter = buffer#get_iter `START in
  buffer#insert ~iter (
    &quot;From: pathfinder@nasa.gov\n&quot; ^
    &quot;To: mom@nasa.gov\n&quot; ^
    &quot;Subject: Made it!\n&quot; ^
    &quot;\n&quot; ^
    &quot;We just got in this morning. The weather has been\n&quot; ^
    &quot;great - clear but cold, and there are lots of fun sights.\n&quot; ^
    &quot;Sojourner says hi. See you soon.\n&quot; ^
    &quot; -Path\n&quot;)

(* Create a scrolled text area that displays a &quot;message&quot; *)
let create_text () =
  let scrolled_window = GBin.scrolled_window
    ~hpolicy:`AUTOMATIC ~vpolicy:`AUTOMATIC () in
  let view = GText.view ~packing:scrolled_window#add () in
  let buffer = view#buffer in
  insert_text buffer;
  scrolled_window#coerce

let main () =
  (* Create a new window; set title and border width *)
  let window = GWindow.window ~title:&quot;Paned Windows&quot; ~border_width:10
    ~width:450 ~height:400 () in

  (* Set a handler for destroy event that immediately exits GTK. *)
  window#connect#destroy ~callback:GMain.Main.quit;

  (* create a vpaned widget and add it to our toplevel window *)
  let vpaned = GPack.paned `VERTICAL ~packing:window#add () in

  (* Now create the contents of the two halves of the window *)
  let list = create_list () in
  vpaned#add1 list;

  let text = create_text () in
  vpaned#add2 text;

  window#show ();
  GMain.Main.main ()

let _ = Printexc.print main ()</code></pre>
<hr />
<table>
<tbody>
<tr class="odd">
<td align="left"><a href="x1538.html">&lt;&lt;&lt; Previous</a></td>
<td align="left"><a href="book1.html">Home</a></td>
<td align="left"><a href="x1577.html">Next &gt;&gt;&gt;</a></td>
</tr>
<tr class="even">
<td align="left">Aspect Frames</td>
<td align="left"><a href="c1436.html">Up</a></td>
<td align="left">Viewports</td>
</tr>
</tbody>
</table>
