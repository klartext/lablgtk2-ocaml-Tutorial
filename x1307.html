
<html><head><title>Calendar</title><meta name="GENERATOR" content="Modular DocBook HTML Stylesheet Version 1.7"/><link rel="HOME" title="GTK+ 2.0 Tutorial using Ocaml" href="book1.html"/><link rel="UP" title="Miscellaneous Widgets" href="c953.html"/><link rel="PREVIOUS" title="Combo Box" href="x1278.html"/><link rel="NEXT" title="Color Selection" href="x1390.html"/></head><body class="SECT1" bgcolor="#FFFFFF" text="#000000" link="#0000FF" vlink="#840084" alink="#0000FF"><div class="NAVHEADER"><table summary="Header navigation table" width="100%" border="0" cellpadding="0" cellspacing="0"><tr><th colspan="3" align="center">GTK+ 2.0 Tutorial using Ocaml</th></tr><tr><td width="10%" align="left" valign="bottom"><a href="x1278.html" accesskey="P">&#60;&#60;&#60; Previous</a></td><td width="80%" align="center" valign="bottom">Miscellaneous Widgets</td><td width="10%" align="right" valign="bottom"><a href="x1390.html" accesskey="N">Next &#62;&#62;&#62;</a></td></tr></table><hr align="LEFT" width="100%"/></div><div class="SECT1"><h1 class="SECT1"><a name="SEC-CALENDAR">Calendar</a></h1><p>The Calendar widget is an effective way to display and retrieve
monthly date related information. It is a very simple widget to create
and work with.</p><p>Creating a GtkCalendar widget is a simple as: (see
<a href="http://lablgtk.forge.ocamlcore.org/refdoc/GMisc.html#VALcalendar" target="_top"><tt class="LITERAL">GMisc.calendar</tt></a>)</p><table border="0" bgcolor="#E0E0E0" width="100%"><tr><td><pre class="PROGRAMLISTING">val GMisc.calendar :
	?options:Gtk.Tags.calendar_display_options list -&#62;
	?packing:(GObj.widget -&#62; unit) -&#62;
	?show:bool -&#62; unit -&#62; calendar</pre></td></tr></table><p>There might be times where you need to change a lot of information
within this widget and the following functions allow you to make
multiple change to a Calendar widget without the user seeing multiple
on-screen updates.</p><table border="0" bgcolor="#E0E0E0" width="100%"><tr><td><pre class="PROGRAMLISTING">method freeze : unit -&#62; unit
method thaw : unit -&#62; unit</pre></td></tr></table><p>They work just like the freeze/thaw functions of every other
widget.</p><p>The Calendar widget has a few options that allow you to change the way
the widget both looks and operates by using the function</p><table border="0" bgcolor="#E0E0E0" width="100%"><tr><td><pre class="PROGRAMLISTING">method display_options : Gtk.Tags.calendar_display_options list -&#62; unit</pre></td></tr></table><p>The <tt class="LITERAL">flags</tt> argument can be formed by combining either of the
following five options:</p><p></p><div class="VARIABLELIST"><dl><dt><tt class="LITERAL">`SHOW_HEADING</tt></dt><dd><p>this option specifies that the month and year should be shown 
when drawing the calendar.</p></dd><dt><tt class="LITERAL">`SHOW_DAY_NAMES</tt></dt><dd><p>this option specifies that the three letter descriptions should 
be displayed for each day (eg Mon,Tue, etc.).</p></dd><dt><tt class="LITERAL">`NO_MONTH_CHANGE</tt></dt><dd><p>this option states that the user
should not and can not change the currently displayed month. This can
be good if you only need to display a particular month such as if you
are displaying 12 calendar widgets for every month in a particular
year.</p></dd><dt><tt class="LITERAL">`SHOW_WEEK_NUMBERS</tt></dt><dd><p>this option specifies that the
number for each week should be displayed down the left side of the
calendar. (eg. Jan 1 = Week 1,Dec 31 = Week 52).</p></dd><dt><tt class="LITERAL">`WEEK_START_MONDAY</tt></dt><dd><p>this option states that the
calander week will start on Monday instead of Sunday which is the
default. This only affects the order in which days are displayed from
left to right.</p></dd></dl></div><p>The following functions are used to set the the currently displayed
date:</p><table border="0" bgcolor="#E0E0E0" width="100%"><tr><td><pre class="PROGRAMLISTING">method select_month : month:int -&#62; year:int -&#62; unit
method select_day : int -&#62; unit</pre></td></tr></table><p>With <tt class="LITERAL">select_day()</tt> the specified day number is
selected within the current month, if that is possible. A
<tt class="LITERAL">day</tt> value of 0 will deselect any current selection.</p><p>In addition to having a day selected, any number of days in the month
may be "marked". A marked day is highlighted within the calendar
display. The following functions are provided to manipulate marked
days:</p><table border="0" bgcolor="#E0E0E0" width="100%"><tr><td><pre class="PROGRAMLISTING">method mark_day : int -&#62; unit
method unmark_day : int -&#62; unit
method clear_marks : unit</pre></td></tr></table><p>For example:</p><table border="0" bgcolor="#E0E0E0" width="100%"><tr><td><pre class="PROGRAMLISTING">let calendar = GMisc.calendar in

...

(* Is day 7 marked? *)
if calendar#mark_day 7 then (
    (* day is marked *)
) else (
    ...
)</pre></td></tr></table><p>Note that marks are persistent across month and year changes.</p><p>The final Calendar widget function is used to retrieve the currently
selected date, month and/or year.</p><table border="0" bgcolor="#E0E0E0" width="100%"><tr><td><pre class="PROGRAMLISTING">method date : int * int * int</pre></td></tr></table><p>The Calendar widget can generate a number of signals indicating date
selection and change. The names of these signals are self explanatory,
and are; see
<a href="http://lablgtk.forge.ocamlcore.org/refdoc/GMisc.calendar_signals.html" target="_top"><tt class="LITERAL">GMisc.calendar_signals</tt></a>:</p><p></p><ul><li><p> <tt class="LITERAL">month_changed</tt></p></li><li><p> <tt class="LITERAL">day_selected</tt></p></li><li><p> <tt class="LITERAL">day_selected_double_click</tt></p></li><li><p> <tt class="LITERAL">prev_month</tt></p></li><li><p> <tt class="LITERAL">next_month</tt></p></li><li><p> <tt class="LITERAL">prev_year</tt></p></li><li><p> <tt class="LITERAL">next_year</tt></p></li></ul><p>That just leaves us with the need to put all of this together into
example code.</p><p><span class="INLINEMEDIAOBJECT"><img src="images/calendar.png"/></span></p><table border="0" bgcolor="#E0E0E0" width="100%"><tr><td><pre class="PROGRAMLISTING">(* file: calendar.ml *)

type signals =
  { mutable last_sig: GMisc.label;
    mutable prev_sig: GMisc.label;
    mutable prev2_sig: GMisc.label
  }

let signals =
  let label = GMisc.label () in
  { last_sig = label;
    prev_sig = label;
    prev2_sig = label }

let set_signal_strings string =
  signals.prev2_sig#set_text signals.prev_sig#text;
  signals.prev_sig#set_text signals.last_sig#text;
  signals.last_sig#set_text string

let show_signal calendar msg () =
  let (y, m, d) = calendar#date in
  let str = Printf.sprintf "%s: %d/%d/%d" msg y m d in
  set_signal_strings str

let toggle_flags calendar but_flags () =
  let rec loop bflags =
    match bflags with
    | [] -&#62; []
    | (but, flag)::rest -&#62;
      if but#active
      then flag :: loop rest
      else loop rest
  in
  let flags = loop but_flags in
  calendar#display_options flags

let font_selection_ok font_window calendar () =
  let font_name = font_window#selection#font_name in
  let font_desc = GPango.font_description font_name in
  calendar#misc#modify_font font_desc

let select_font calendar () =
  let fwin = GWindow.font_selection_dialog ~title:"Font Selection Dialog" ~modal:true ~position:`MOUSE () in
  fwin#connect#destroy ~callback:fwin#destroy;
  fwin#ok_button#connect#clicked ~callback:(font_selection_ok fwin calendar);
  fwin#cancel_button#connect#clicked ~callback:fwin#destroy;
  fwin#show ()

let flags = [("Show Heading", true, `SHOW_HEADING);
    ("Show Day Names", true, `SHOW_DAY_NAMES);
    ("No Month Change", false, `NO_MONTH_CHANGE);
    ("Show Week Numbers", false, `SHOW_WEEK_NUMBERS);
    ("Week Start Monday", false, `WEEK_START_MONDAY)]

let create_calendar () =
  (* Create a new window; set title and border width *)
  let window = GWindow.window ~title:"Calendar Example" ~resizable:false ~border_width:10 () in

  (* Set a handler for destroy event that immediately exits GTK. *)
  window#connect#destroy ~callback:GMain.Main.quit;

  let vbox = GPack.vbox ~border_width:10 ~packing:window#add () in

  (* The top part of the window, Calendar, flags and fontsel. *)
  let hbox = GPack.hbox ~packing:vbox#add () in
  let hbbox = GPack.button_box `HORIZONTAL ~layout:`SPREAD ~spacing:5 ~packing:hbox#add () in

  (* Calendar widget *)
  let frame = GBin.frame ~packing:hbbox#add () in
  let calendar = GMisc.calendar ~packing:frame#add () in
  calendar#mark_day 19;
  calendar#connect#month_changed ~callback:(show_signal calendar "month_changed");
  calendar#connect#day_selected ~callback:(show_signal calendar "day_selected");
  calendar#connect#day_selected_double_click ~callback:(show_signal calendar "day_selected_double_click");
  calendar#connect#prev_month ~callback:(show_signal calendar "prev_month");
  calendar#connect#next_month ~callback:(show_signal calendar "next_month");
  calendar#connect#prev_year ~callback:(show_signal calendar "prev_year");
  calendar#connect#next_year ~callback:(show_signal calendar "next_year");

  let separator = GMisc.separator `VERTICAL ~packing:hbox#add () in

  let vbox2 = GPack.vbox ~packing:hbox#add () in

  (* Build the Right frame with the flags in *)
  let frame = GBin.frame ~label:"Flags" ~packing:vbox2#add () in
  let vbox3 = GPack.vbox ~packing:frame#add () in
  let toggle_button (label, active, flag) =
    (GButton.check_button ~label ~active ~packing:vbox3#add (), flag) in
  let flag_buttons = List.map toggle_button flags in
  let set_flag_cb (but, _) =
    but#connect#toggled ~callback:(toggle_flags calendar flag_buttons);
    ()
  in
  List.iter set_flag_cb flag_buttons;

  (* Build the right font-button *)
  let button = GButton.button ~label:"Font..." ~packing:vbox2#add () in
  button#connect#clicked ~callback:(select_font calendar);

  (* Build the Signal-event part. *)
  let frame = GBin.frame ~label:"Signal events" ~packing:vbox#add () in
  let vbox2 = GPack.vbox ~packing:frame#add () in

  let hbox = GPack.hbox ~packing:vbox2#add () in
  let label = GMisc.label ~text:"Signal: " ~packing:hbox#add () in
  signals.last_sig &#60;- GMisc.label ~packing:hbox#add ();

  let hbox = GPack.hbox ~packing:vbox2#add () in
  let label = GMisc.label ~text:"Previous signal: " ~packing:hbox#add () in
  signals.prev_sig &#60;- GMisc.label ~packing:hbox#add ();

  let hbox = GPack.hbox ~packing:vbox2#add () in
  let label = GMisc.label ~text:"Second previous signal: " ~packing:hbox#add () in
  signals.prev2_sig &#60;- GMisc.label ~packing:hbox#add ();

  let bbox = GPack.button_box `HORIZONTAL ~layout:`END ~packing:vbox#add () in
  let button = GButton.button ~label:"Close" ~packing:bbox#add () in
  button#connect#clicked ~callback:GMain.Main.quit;

  button#misc#set_can_default true;
  button#misc#grab_default ();

  window#show ()

let main () =
  create_calendar ();

  (* Enter the event loop *)
  GMain.Main.main ()

let _ = Printexc.print main ()</pre></td></tr></table></div><div class="NAVFOOTER"><hr align="LEFT" width="100%"/><table summary="Footer navigation table" width="100%" border="0" cellpadding="0" cellspacing="0"><tr><td width="33%" align="left" valign="top"><a href="x1278.html" accesskey="P">&#60;&#60;&#60; Previous</a></td><td width="34%" align="center" valign="top"><a href="book1.html" accesskey="H">Home</a></td><td width="33%" align="right" valign="top"><a href="x1390.html" accesskey="N">Next &#62;&#62;&#62;</a></td></tr><tr><td width="33%" align="left" valign="top">Combo Box</td><td width="34%" align="center" valign="top"><a href="c953.html" accesskey="U">Up</a></td><td width="33%" align="right" valign="top">Color Selection</td></tr></table></div></body></html>